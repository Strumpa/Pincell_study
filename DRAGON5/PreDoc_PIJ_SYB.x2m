* Cle2000 input deck for Pre-Doctoral wirtten exam question
* R. Guasch
* Purpose : PIJ calculation for pincell with given MACRO sections
* Two appoaches : given PIJ and isotropic BC, use NXT TISO and SYBILT and compare.
* SYBILT is a-priori assumed to be closer to MATLAB solution with sybt2d.m

MODULE 
    GEO: MAC: SYBILT: NXT: ASM: FLU: GREP: ;
LINKED_LIST 
    MACRO GEOM TRACK_S SYS FLUX EDITION ;

REAL V1 V2 V3 V4 V5 := 0.4 0.7 0.4 1.3 2.1 ;
REAL pitch ;
EVALUATE pitch := 4.9 SQRT ;
REAL R1 R2 R3 R4 := 0.356825 0.591727 0.690988 0.94407 ;
REAL Keff1 ;

* Definition of Macroscopic cross sections as specified by table 1.
MACRO := MAC: ::
    NGRO 1 NMIX 5 NIFI 1
    READ INPUT
    MIX 1   TOTAL 0.2
            SCAT   1 1  0.05
            NUSIGF  1.4  CHI 1.0 
    MIX 2   TOTAL 0.0
    MIX 3   TOTAL 0.5
            SCAT   1 1  0.05
    MIX 4   TOTAL 0.0
    MIX 5   TOTAL 0.3
            SCAT   1 1  0.05 
  ;

* Pincell geometry definition, 
GEOM := GEO: :: CARCEL 4
    X- REFL X+ REFL
    Y- REFL Y+ REFL
    RADIUS 0.0 <<R1>> <<R2>> <<R3>> <<R4>>
    MIX 1 2 3 4 5 
    MESHX 0.0 <<pitch>> MESHY 0.0 <<pitch>>
;

* Parameters and tracking with SYBILT:
INTEGER iqua2 := 24 ;
INTEGER nseg := 10 ;
TRACK_S := SYBILT: GEOM ::
    MAXR 50
    MAXZ 2000000
    TITLE 'TRACKING FOR PINCELL'
    QUA2 <<iqua2>> <<nseg>> GAUS 
;
SYS := ASM: MACRO TRACK_S :: PIJ EDIT 0 ; 
FLUX := FLU: MACRO SYS TRACK_S :: 
        EDIT 1 TYPE K ;

GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1  >>Keff1<< ;
ECHO "tracking = SYBILT, CP solution Keff=" Keff1 ;
